pkgbase='llvm-project'
pkgname=('compiler-rt' 'libunwind' 'libunwind-dev' 'libcxxabi' 'libcxxabi-dev' 'libcxx' 'libcxx-dev' 'libclang' 'libclang-dev' 'clang' 'lld-dev' 'lld' 'llvm-lib' 'llvm-dev' 'llvm')
pkgver=11.0.0.r355960.g81826c7a
pkgrel=1
arch=('x86_64')
url='https://llvm.org/'
license=('custom:University of Illinois')
makedepends=('python' 'zlib-ng-dev' 'libclang-dev' 'cmake' 'samurai')
source=('git://github.com/llvm/llvm-project.git'
        'musl.patch')
sha256sums=('SKIP'
            '4cb1bb13f5da4d2e3687a036fcb9595eb234d3561035d8a1b3736f3634fe14dc')

pkgver() {
  cd "${pkgbase}/llvm"
  
  echo $(awk -F 'MAJOR |MINOR |PATCH |)' \
            'BEGIN { ORS="." ; i=0 } \
             /set\(LLVM_VERSION_/ { print $2 ; i++ ; if (i==2) ORS="" } \
             END { print "\n" }' \
        CMakeLists.txt).r$(git rev-list --count master).g$(git rev-parse HEAD | cut -c1-8)
}

prepare() {
  cd "${pkgbase}"
  
  patch -Np1 -i "${srcdir}/musl.patch"
}

build() {
  cd "${pkgbase}"

  if [ ! -d "build" ]; then
    mkdir "build"
  fi
  
  cd "build"
  
  CFLAGS=${CFLAGS//'-flto=thin'/'-flto'}
  CXXFLAGS=${CXXFLAGS//'-flto=thin'/'-flto'}
  LDFLAGS=${LDFLAGS//'-flto=thin'/'-flto'}
  
  cmake -G 'Ninja' \
        -DCMAKE_BUILD_TYPE='Release' \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_C_COMPILER=clang \
        -DCMAKE_CXX_COMPILER=clang++ \
        \
        -DLLVM_DEFAULT_TARGET_TRIPLE=$CHOST \
        -DLLVM_ENABLE_LIBCXX=ON \
        -DLLVM_LINK_LLVM_DYLIB=ON \
        -DLLVM_ENABLE_BACKTRACES=OFF \
        -DLLVM_ENABLE_LIBEDIT=OFF \
        -DLLVM_ENABLE_LIBXML2=OFF \
        -DLLVM_ENABLE_LLD=ON \
        -DLLVM_ENABLE_TERMINFO=OFF \
        -DLLVM_ENABLE_ZLIB=ON \
        -DLLVM_INSTALL_UTILS=ON \
        -DLLVM_TARGETS_TO_BUILD=X86 \
        -DLLVM_ENABLE_PROJECTS='clang;compiler-rt;libcxx;libcxxabi;libunwind;lld' \
        \
        -DCLANG_DEFAULT_CXX_STDLIB='libc++' \
        -DCLANG_DEFAULT_RTLIB='compiler-rt' \
        -DCLANG_PLUGIN_SUPPORT=OFF \
        \
        -DCOMPILER_RT_BUILD_SANITIZERS=OFF \
        -DCOMPILER_RT_BUILD_XRAY=OFF \
        -DCOMPILER_RT_INCLUDE_TESTS=OFF \
        \
        -DLIBCXX_CXX_ABI='libcxxabi' \
        -DLIBCXX_CXX_ABI_INCLUDE_PATHS="${srcdir}/libcxxabi/include" \
        -DLIBCXX_HAS_MUSL_LIBC=ON \
        -DLIBCXX_USE_COMPILER_RT=ON \
        \
        -DLIBCXXABI_USE_LLVM_UNWINDER=ON \
        -DLIBCXXABI_USE_COMPILER_RT=ON \
        \
        -DLIBUNWIND_USE_COMPILER_RT=ON \
        \
        -DSANITIZER_CXX_ABI='libcxxabi' \
        \
        "../llvm"
  
  samu
}

package(){
  cd "${pkgbase}/build"
  
  sed -i '/clang\/cmake_install.cmake/d' "tools/cmake_install.cmake"
  sed -i '/lld\/cmake_install.cmake/d' "tools/cmake_install.cmake"
  
  sed -i '/compiler-rt\/cmake_install.cmake/d' "projects/cmake_install.cmake"
  sed -i '/libcxxabi\/cmake_install.cmake/d' "projects/cmake_install.cmake"
  sed -i '/libcxx\/cmake_install.cmake/d' "projects/cmake_install.cmake"
  sed -i '/libunwind\/cmake_install.cmake/d' "projects/cmake_install.cmake"
}

package_compiler-rt() {
  pkgdesc='LLVM compiler-rt runtime libraries'
  
  cd "${pkgbase}/build"
  
  DESTDIR="${pkgdir}" samu projects/compiler-rt/install
}

package_libunwind(){
  depends=('musl')
  
  cd "${pkgbase}/build"
  
  package-init
  
  DESTDIR="${tmpdir}" samu projects/libunwind/install
  
  package-lib
}

package_libunwind-dev() {
  depends=('libunwind')
  
  package-base
}

package_libcxxabi(){
  depends=('musl' 'libunwind')
  
  cd "${pkgbase}/build"
  
  package-init
  
  DESTDIR="${tmpdir}" samu projects/libcxxabi/install
  
  package-lib
}

package_libcxxabi-dev() {
  depends=('libcxxabi' 'libunwind-dev')
  
  package-base
}

package_libcxx() {
  depends=('musl' 'libunwind' 'libcxxabi')
  
  cd "${pkgbase}/build"
  
  package-init
  
  DESTDIR="${tmpdir}" samu projects/libcxx/install
  
  package-lib
}

package_libcxx-dev() {
  depends=('libcxx' 'libcxxabi-dev')
  
  package-base
}

package_libclang() {
  depends=('musl' 'libcxx' 'libcxxabi' 'llvm-lib')
  
  cd "${pkgbase}/build"
  
  package-init
  
  DESTDIR="${tmpdir}" samu tools/clang/install
  
  package-lib
}

package_libclang-dev() {
  depends=('libclang')
  
  package-dev
}

package_clang() {
  depends=('libclang' 'crt' 'musl-dev' 'compiler-rt' 'libunwind-dev' 'libcxxabi-dev' 'libcxx-dev' 'lld')
  groups=('base-devel')
  
  package-base
}

package_lld-dev() {
  depends=('lld')
  
  cd "${pkgbase}/build"
  
  package-init
  
  DESTDIR="${tmpdir}" samu tools/lld/install
  
  package-dev
}

package_lld() {
  depends=('musl' 'libcxxabi' 'libcxx' 'llvm-lib')
  
  package-base
  
  ln -s lld "${pkgdir}/usr/bin/ld"
}

package_llvm-lib() {
  depends=('musl' 'libunwind' 'libcxxabi' 'libcxx' 'zlib')
  
  cd "${pkgbase}/build"
  
  package-init
  
  DESTDIR="${tmpdir}" samu install
  
  package-lib
  
  mv ${tmpdir}/usr/lib/libLLVM-*.so "${pkgdir}/usr/lib/"
}

package_llvm-dev() {
  depends=('llvm')
  
  package-dev
}

package_llvm() {
  depends=('musl' 'libunwind' 'libcxxabi' 'libcxx' 'llvm-lib')
  
  package-base
}
